#!/bin/csh -f
# $Id: sarsnap.in,v 1.6 2009/12/29 19:15:47 abeard Exp $
#
# Script to create HTML files containing 10 day history of
# sar plots for machines which have sar running.
#
# This script is run by cron as specified in /etc/cron.d/sar.
#
# @author Marc Pound

# document root for apache installation
set docroot = "/array/utilities/apache2.0.54/htdocs"

# age after which to delete thumbnails in days.
set ageafter = 90

if ( $#argv != 1 ) then
  goto Usage
endif

# machines which currently have sar enabled.
set machine = "$1"

# location of sarplots for machine listed above
set sarlocation = "$docroot/RTS/sar/$machine"

# URL of the daily sarplots for machine listed above
set url = "http://cedarflat.mmarray.org/RTS/sar/$machine"

if ( ! -d $sarlocation ) then
  mkdir $sarlocation
endif

set m=$machine
set s=$sarlocation
set urlroot=$url

#echo got machine $m
#echo got location $s
#echo got urlroot $urlroot


set outdisk=/home/mpound/public_html
set outfile=$outdisk/"tmp_"$m".html"
set finalfile=$outdisk/"sar10_"$m".html"
set mwpurlroot="http://www.mmarray.org/~mpound/sar/$machine"
set mwproot="/home/mpound/public_html/sar/$machine"

echo "<HTML><HEAD><TITLE>SAR plots for $m last 10 days</TITLE></HEAD><BODY>" > $outfile
echo "<CENTER><h1>$m sar plots for last 10 days</h1></center>"  >> $outfile
# last 10 days
set last = ( `ls -rt $s | tail | tr "/" " " | sort -nr` )

if ( ! -d $mwproot ) then
    mkdir -p $mwproot
endif

set root=$s
foreach dir ( $last )
 set images = ( `ls $root/$dir/*.png` ) 
 echo "<h2>$dir</h2>"  >> $outfile
 echo "<table cols=10><tr>"  >> $outfile
 foreach file ( $images )
     set base=`basename $file`
     set out=$dir'_'$base
     /usr/bin/convert $file -thumbnail 160x120 PNG8:$mwproot/$out
     echo "<td><a href=$urlroot/$dir/$base>$base" >> $outfile
     echo "<img width=160 height=120 src=$mwpurlroot/$out></a></td>" >> $outfile
 end
 echo "</tr></table><hr>" >> $outfile
end
 
echo "Generated by $0 on: "`date` >>$outfile
echo "</BODY></HTML>" >> $outfile

mv $outfile $finalfile

# Remove thumbnails older than $ageafter.
/usr/bin/find $mwproot -maxdepth 1 -mtime +$ageafter -type f -exec rm -fr \{\} \;

TheEnd:
exit 0

Usage:
 echo "Usage: $0 <sar subdir>"
